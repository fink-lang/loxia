{
  blockStatement, variableDeclaration, variableDeclarator, doExpression
  arrayExpression, assignmentExpression, callExpression
  memberExpression, identifier, expressionStatement, arrayPattern, restElement
  numericLiteral
} = import '@babel/types'
{length, is_empty} = import '@fink/std-lib/iter.fnk'
{wrap_with_comment_loc} = import '../comments/init.fnk'


{transform_left} = import '../../js/left.fnk'
{add, any} = import '../context.fnk'
{transform_value} = import '../partial/init.fnk'


# TODO: std-lib
find_index = fn iterable, func:
  iterable.findIndex func


has_spread_not_last = fn {left}:
  match left:
    {type: ? in ['array', 'list'], exprs: 1 < length ?}:
      idx = find_index left.exprs, fn expr:
        match expr:
          {type: 'spread'}: true
          else: false

      exprs_len = length left.exprs
      -1 < idx < exprs_len - 1
    else:
      false


transform_spread_left = fn {left}, {transform}:
  [before, middle, end] = pipe left.exprs:
    fold expr, [before=[], middle=false, end=[]]=[]:
      match expr:
        {type: 'spread', right: {}}:
          [before, expr.right, end]

        {type: 'spread'}:
          [before, {type: 'empty'}, end]

        else:
          match middle:
            false: [[...before, expr], middle, end]
            else: [before, middle, [...end, expr]]

  exprs = match before:
    is_empty ?:
      # TODO missing loc
      [middle, {type: 'list', exprs: end}]
    else:
      # TODO missing loc
      [{type: 'list', exprs: before}, middle, {type: 'list', exprs: end}]

  transform_left transform {...left, exprs}


slice = fn items, start, end=false:
  start_num = numericLiteral start

  start_end = match end:
    false:
      [start_num]
    else:
      end_num = numericLiteral end
      [start_num, end_num]

  callExpression
    memberExpression
      items
      identifier 'slice'
    start_end


transform_spread_right = fn expr, ctx:
  {left, right} = expr
  items = ctx.unique_ident 'items'
  # TODO: wrap declarator?
  items_init = wrap_with_comment_loc
    variableDeclaration
      'const'
      list:
        variableDeclarator
          arrayPattern [restElement items]
          transform_value right, ctx
    left

  len = (length left.exprs) - 1
  idx = find_index left.exprs, fn expr:
    match expr:
      {type: 'spread'}: true
      else: false

  result_items = match idx:
    0: list:
      slice items, idx, idx - len
      slice items, idx - len

    else: list:
      items
      slice items, idx, idx - len
      slice items, idx - len

  result = expressionStatement
    arrayExpression result_items

  doExpression
    wrap_with_comment_loc
      blockStatement [items_init, result]
      left


transform_assign = fn node, ctx:
  match node:
    has_spread_not_last ?:
      left = transform_spread_left node, ctx
      right = transform_spread_right node, ctx
      assignmentExpression '=', left, right

    else:
      left = transform_left ctx.transform node.left
      right = transform_value node.right, ctx
      assignmentExpression '=', left, right


add_assignment = fn ctx:
  pipe ctx:
    add any, '=', transform_assign

