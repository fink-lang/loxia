babel_types = import '@babel/types'
{
  blockStatement, variableDeclaration, variableDeclarator, doExpression
  arrayExpression, assignmentExpression, callExpression
  memberExpression, identifier, expressionStatement, arrayPattern, restElement
  numericLiteral, awaitExpression, logicalExpression
} = babel_types
{map, map_ac, filter, fold, length, is_empty} = import '@fink/std-lib/iter.fnk'

{unique_ident} = import '../../js/types.fnk'
{transform_left: xform_left} = import '../../js/left.fnk'
{wrap_with_comment_loc} = import '../comments/init.fnk'
{add, any} = import '../context.fnk'
{transform_value} = import '../partial/init.fnk'
{transform} = import '../transform.fnk'



transform_left = fn node, ctx:
  [left, next_ctx] = transform node, {...ctx, is_binding: true}
  js = xform_left left
  [js, next_ctx]



has_spread_not_last = fn {left}:
  match left:
    # {type: 'list', exprs: [..., {type: 'spread'}, ..., {type: ? != 'spread'}]}:
    #   true
    {type: 'list'}:
      [...exprs, ] = left.exprs

      [spread=false] = pipe exprs:
        # TODO map ?.type == 'spread'
        map fn {type}: type == 'spread'
        #TODO filter is_spread
        filter fn is_spread: is_spread

      spread
    else:
      false


transform_spread_left = fn {left}, ctx:
  [before, middle, end] = pipe left.exprs:
    fold [], fn expr, [before=[], middle=false, end=[]]:
      match expr:
        {type: 'spread', right: false}:
          [before, {type: 'empty'}, end]

        {type: 'spread', right: {}}:
          [before, expr.right, end]

        else:
          match middle:
            false: [[...before, expr], middle, end]
            else: [before, middle, [...end, expr]]

  exprs = match before:
    is_empty ?:
      # TODO missing loc
      [middle, {type: 'list', exprs: end}]
    else:
      # TODO missing loc
      [{type: 'list', exprs: before}, middle, {type: 'list', exprs: end}]

  transform_left {...left, exprs}, ctx



slice = fn items, start, end=false:
  start_num = numericLiteral start

  start_end = match end:
    false:
      [start_num]
    else:
      end_num = numericLiteral end
      [start_num, end_num]

  callExpression
    memberExpression
      items
      identifier 'slice'
    start_end



transform_spread_right = fn expr, ctx:
  {left, right} = expr
  [items, init_ctx] = unique_ident 'items', ctx
  # TODO: wrap declarator?
  [init, next_ctx] = transform_value right, init_ctx

  items_init = wrap_with_comment_loc
    variableDeclaration
      'const'
      list:
        variableDeclarator
          arrayPattern [restElement items]
          init
    left

  len = (length left.exprs) - 1
  [[idx]] = pipe left.exprs:
    map_ac fn expr, idx=0:
      [[idx, expr], idx + 1]
    filter fn [, {type}]:
      type == 'spread'

  slices = list:
    slice items, idx, idx - len
    slice items, idx - len

  result_items = match idx:
    0: slices
    else: [items, ...slices]

  js = doExpression
    wrap_with_comment_loc
      blockStatement list:
        items_init
        expressionStatement arrayExpression result_items
      left
  [js, next_ctx]



has_await = fn node:
  match node:
    {left: {type: 'list', exprs: [{op: 'await'}]}}:
      true
    else:
      false



transform_await_left = fn node, ctx:
  {left: {exprs: [{right: expr}, ...rest_exprs], ...rest_left}} = node
  left = {...rest_left, exprs: [expr, ...rest_exprs]}
  transform_left left, ctx




transform_await_right = fn expr, ctx:
  {left, right} = expr
  [items, iter_ctx] = unique_ident 'items', ctx
  [iter, init_ctx] = unique_ident 'iter', iter_ctx

  [init, next_ctx] = transform_value right, init_ctx

  items_init = wrap_with_comment_loc
    variableDeclaration 'const', [variableDeclarator items, init]
    right

  iter_init = wrap_with_comment_loc
    variableDeclaration
      'const'
      list:
        variableDeclarator
          iter
          callExpression
            memberExpression
              rec:
                ...logicalExpression
                  '||'
                  memberExpression
                    items
                    memberExpression
                      identifier 'Symbol'
                      identifier 'asyncIterator'
                    true
                  memberExpression
                    items
                    memberExpression
                      identifier 'Symbol'
                      identifier 'iterator'
                    true
                leadingComments: [{type: 'CommentBlock', value: 'istanbul ignore next'}]
              identifier 'call'
            [items]
    right

  [...right_exprs] = pipe left.exprs:
    map fn expr:
      wrap_with_comment_loc
        memberExpression
          awaitExpression
            callExpression
              memberExpression
                iter
                identifier 'next'
              []
          identifier 'value'
        expr

  js = doExpression
    wrap_with_comment_loc
      blockStatement list:
        items_init
        iter_init
        expressionStatement
          arrayExpression right_exprs
      right

  [js, next_ctx]



transform_assign_left = fn node, ctx:
  transform_left node.left, ctx



transform_assign_right = fn node, ctx:
   transform_value node.right, ctx




transform_assign = fn node, ctx:
  [left_transform, right_transform] = match node:
    has_await ?:
      [transform_await_left, transform_await_right]

    has_spread_not_last ?:
      [transform_spread_left, transform_spread_right]

    else:
      [transform_assign_left, transform_assign_right]


  [left, right_ctx] = left_transform node, ctx
  [right, next_ctx] = right_transform node, {...right_ctx, is_binding: false}

  js = assignmentExpression '=', left, right
  [js, next_ctx]



add_assignment = fn ctx:
  pipe ctx:
    add any, '=', transform_assign

