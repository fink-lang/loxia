{is_str} = import '@fink/std-lib/str.fnk'

{wrap} = import '../js/types.fnk'

{transform_error} = import './errors.fnk'
{get_transformer} = import './context.fnk'

{wrap_with_comment_loc} = import './comments/init.fnk'



add_ignorable_import = fn uri: fn ctx:
  {...ctx, ignoreable_imports: [...ctx.ignoreable_imports, uri]}



is_ignorable_import = fn expr, ctx:
  match expr:
    {op: 'import', right: {exprs: [{value: is_str ?}]}}:
      [{value: uri}] = expr.right.exprs
      uri in ctx.ignoreable_imports
    else:
      false


wrap_node = fn ctx, js_node, node:
  match ctx:
    {wrap: 'loc'}:
      wrap node, js_node
    else:
      wrap_with_comment_loc js_node, node


map_with_ctx = fn fnc:
  map item, , ctx:
    [value, next_ctx] = fnc item, ctx
    [value, , next_ctx]


# TODO: std-lib
drop_if = fn cond: filter item, , ctx:
  keep = not cond item, ctx
  [keep, , ctx]



collect_with_ctx = fn ctx:
  fold item, items=[], , curr_ctx=ctx:
    [[...items, item], , curr_ctx]



transform_with_comments = fn transform_expr, node, ctx:
  [err, result] = try:
    transform_expr node, ctx

  match err:
    false:
      [js_node, next_ctx] = result
      js = wrap_node next_ctx, js_node, node
      [js, next_ctx]
    {transform_error: {}}:
      throw err
    else:
      throw transform_error err, node, ctx



transform = fn node, ctx:
  transform_expr = get_transformer node, ctx

  match transform_expr:
    false:
      throw transform_error 'Unknown expression.', node, ctx
    else:
      transform_with_comments transform_expr, node, ctx

