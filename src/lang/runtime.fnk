


add_runtime_fn = fn name, ctx:
  {runtime} = ctx
  rec:
    ...ctx
    runtime: rec:
      ...runtime
      names: [...runtime.names, name]




set_default_runtime_impl = fn name, uri, ctx:
  {runtime} = ctx
  rec:
    ...ctx
    runtime: rec:
      ...runtime
      defaults: rec: ...runtime.defaults, (name): uri



set_runtime_impl = fn name, uri, ctx:
  {runtime} = ctx
  rec:
    ...ctx
    runtime: rec:
      ...runtime
      overrides: rec: ...runtime.overrides, (name): uri



use_runtime_fn = fn name, ctx:
  {runtime} = ctx
  rec:
    ...ctx
    runtime: rec:
      ...runtime
      used: seq: name, ...runtime.used | filter used_name: used_name != name



has_runtime_override = fn ident, ctx:
  match ctx:
    {runtime: {overrides: {(ident): {}}}}: true
    else: false



runtime_impl = fn ident, ctx:
  match ctx:
    {runtime: {overrides: {(ident): {}}}}:
      {runtime: {overrides: {(ident): uri}}} = ctx
      uri
    else:
      {runtime: {defaults: {(ident): uri}}} = ctx
      uri



is_runtime_fn = fn name, ctx:
  name in [...ctx.runtime.names]



get_runtime_imports = fn ctx:
  {runtime} = ctx
  pipe runtime.used:
    filter name:
      not has_runtime_override name, ctx

    map name:
      uri = runtime_impl name, ctx
      left = {type: 'ident', value: name}
      [uri, {type: 'rec:kv', left, right: left}]

    map [uri, kv]:
      rec:
        type: 'assign'
        op: '='
        left: {type: 'rec', exprs: [kv]}
        right: rec:
          op: 'import'
          right: rec:
            type: 'string'
            exprs: [{type: 'string:text', value: uri}]
    [...?]



init_builtins = fn ctx:
  rec:
    ...ctx
    runtime: rec:
      names: []
      used: []
      overrides: {}
      defaults: {}
