babel_types = import '@babel/types'
{
  jsxElement, jsxOpeningElement, jsxIdentifier, jsxClosingElement, jsxAttribute
  stringLiteral, jsxExpressionContainer, jsxText, jsxFragment, jsxOpeningFragment
  jsxClosingFragment, jsxSpreadAttribute
} = babel_types

{length} = import '@fink/std-lib/iter.fnk'

{add, any} = import '../context.fnk'
{transform, map_with_ctx, collect_with_ctx} = import '../transform.fnk'



transform_jsx_elem = fn node, ctx:
  id =  match node.name:
    --- istanbul ignore next ---
    {type: 'ident'}: jsxIdentifier node.name.value
    else: jsxIdentifier node.name

  [attrs, children_ctx] = pipe node.props:
    map_with_ctx fn expr, expr_ctx:
      match expr:
        {type: 'spread'}:
          [attr, next_ctx] = transform expr.right, expr_ctx
          spread = jsxSpreadAttribute attr
          [spread, next_ctx]
        else:
          transform expr, expr_ctx
    collect_with_ctx ctx

  [children, next_ctx] = pipe node.children:
    map_with_ctx transform
    collect_with_ctx children_ctx

  js = jsxElement
    jsxOpeningElement id, attrs, node.self_closing
    jsxClosingElement id
    children

  [js, next_ctx]



transform_jsx_frag = fn node, ctx:
  [children, next_ctx] = pipe node.children:
    map_with_ctx transform
    collect_with_ctx ctx

  js = jsxFragment
    jsxOpeningFragment _
    jsxClosingFragment _
    children

  [js, next_ctx]



transform_jsx_attr = fn node, ctx:
  {loc, name} = node

  [value, next_ctx] = match node:
    {value: {type: 'string', exprs: 1 == length ?}}:
      {exprs: [str]} = node.value
      [[stringLiteral str.value], ctx]

    {value: {type: 'jsx:expr'}}:
      [val, next_ctx] = transform node.value, ctx
      [[val], next_ctx]

    --- istanbul ignore next ---
    {value: false}:
      [val, next_ctx] = transform name, ctx
      [[jsxExpressionContainer val], next_ctx]

    {value: {}}:
      [val, next_ctx] = transform node.value, ctx
      [[jsxExpressionContainer val], next_ctx]

    else:
      [val, next_ctx] = transform {type: 'ident', value: name, loc}, ctx
      [[jsxExpressionContainer val], next_ctx]

  id = match name:
    --- istanbul ignore next ---
    {type: 'ident'}: jsxIdentifier name.value
    else: jsxIdentifier name
  js = jsxAttribute id, ...value

  [js, next_ctx]



transform_jsx_text = fn node, ctx:
  js = jsxText node.value
  [js, ctx]



transform_jsx_expr_container = fn node, ctx:
  [val, next_ctx] = transform node.expr, ctx
  js = jsxExpressionContainer val
  [js, next_ctx]



add_jsx = fn ctx:
  pipe ctx:
    add 'jsx:frag', any, transform_jsx_frag
    add 'jsx:elem', any, transform_jsx_elem
    add 'jsx:attr', any, transform_jsx_attr
    add 'jsx:text', any, transform_jsx_text
    add 'jsx:expr', any, transform_jsx_expr_container

