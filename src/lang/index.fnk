{count} = import '@fink/std-lib/iter'

{ident, wrap} = import '../js/types'
{var_prefix} = import '../js/identifier'

{transform_error} = import './errors'
{get_transformer} = import './context'

{add_assignment} = import './assignment'
{add_func} = import './func'
{add_conditionals} = import './conditionals'
{add_iterables} = import './iterable'
{add_call} = import './call'
{add_literals} = import './literals'
{add_spread} = import './spread'
{add_async} = import './async'
{add_logical} = import './logical'
{add_group} = import './group'
{add_module} = import './module'
{add_member} = import './prop-access'
{add_ident} = import './identifier'
{add_comparison} = import './comparison'
{add_jsx} = import './jsx'
{add_js_compat} = import './js-compat'
{add_arithmitic} = import './arithmitic'
{add_block} = import './block'
{add_partial} = import './partial'
{wrap_with_comment_loc} = import './comments'


add_transformers = fn ctx:
  pipe ctx:
    add_module
    add_ident
    add_partial
    add_literals
    add_group
    add_member
    add_logical
    add_comparison
    add_arithmitic
    add_assignment
    add_spread
    add_async
    add_func
    add_conditionals
    add_iterables
    add_call
    add_async
    add_jsx
    add_block
    add_js_compat


wrap_node = fn ctx, js_node, node:
  match ctx:
    {wrap: 'loc'}:
      wrap node, js_node
    else:
      wrap_with_comment_loc js_node, node


try_transform = fn transform, node, ctx:
  [err, js_node] = try: transform node, ctx

  match err:
    false:
      js_node
    else:
      transform_error err, node, ctx


transform_with_comments = fn transform, node, ctx:
  js_node = try_transform transform, node, ctx
  match js_node:
    {error: {}}:
      # TODO don't use mutable
      ctx.errors.push js_node
      js_node
    else:
      wrap_node ctx, js_node, node


transform_expr = fn node, ctx:
  transform_ctx = dict:
    ...ctx
    transform: fn next_node, next_ctx=transform_ctx:
      transform_expr next_node, next_ctx

  transform = get_transformer node, ctx

  match transform:
    false:
      err = transform_error 'Unknown expression.', node, ctx
      # TODO don't use mutable
      ctx.errors.push err
      err
    else:
      transform_with_comments transform, node, transform_ctx


init_ctx = fn code, filename:
  id_cntr = count 1

  ctx = dict:
    filename
    code
    errors: []
    unique_ident: fn name:
      # TODO: return ident and new ctx to make it not stateful
      {value} = id_cntr.next ()
      ident '${var_prefix}${name}_${value}'

  pipe ctx:
    add_transformers


transform_ast = fn expr, code, filename:
  ctx = init_ctx code, filename
  js_ast = transform_expr expr, ctx

  match js_ast:
    {error: {}}:
      {errors: ctx.errors}
    else:
      {...js_ast, errors: []}


