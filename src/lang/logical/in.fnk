babel_types = import '@babel/types'
{identifier, optionalCallExpression, optionalMemberExpression, memberExpression, callExpression, logicalExpression} = babel_types

{transform} = import '../transform.fnk'

{use_builtin} = import '../builtins.fnk'
{builtin_provider} = import '../builtins.fnk'



legacy_uri = '@fink/loxia:iter:legacy'

default_in_impl_uri = legacy_uri



transform_legacy = fn node, ctx:
  {left, right} = node
  [js_right, next_ctx] = transform right, ctx
  [js_left, end_ctx] = transform left, next_ctx


  js = match right:
    {type: 'list'}:
      callExpression
        memberExpression
          js_right
          identifier 'includes'
        [js_left]

    {type: 'string'}:
      callExpression
        memberExpression
          js_right
          identifier 'includes'
        [js_left]

    {type: 'rec'}:
      callExpression
        memberExpression
          js_right
          identifier 'hasOwnProperty'
        [js_left]

    else:
      logicalExpression
        '??'
        optionalCallExpression
          optionalMemberExpression
            js_right
            identifier 'includes'
            false
            true
          [js_left]
          true

        logicalExpression
          '??'
          optionalCallExpression
            optionalMemberExpression
              js_right
              identifier 'has'
              false
              true
            [js_left]
            true

          logicalExpression
            '??'
            optionalCallExpression
              optionalMemberExpression
                js_right
                identifier 'hasOwnProperty'
                false
                true
              [js_left]
              true
            identifier 'false'

  [js, end_ctx]



transform_with_runtime_lib = fn runtime_fn, node, ctx:
  [right, next_ctx] = transform node.right, ctx
  [left, end_ctx] = transform node.left, next_ctx

  js = callExpression
    identifier runtime_fn
    [left, right]

  [js, end_ctx]



transform_in = fn node, ctx:
  runtime_fn = '_${node.op}_'
  next_ctx = use_builtin runtime_fn, ctx

  match next_ctx:
    legacy_uri == builtin_provider runtime_fn, ?:
      transform_legacy node, next_ctx

    else:
      transform_with_runtime_lib runtime_fn, node, next_ctx




