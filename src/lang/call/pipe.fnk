babel_types = import '@babel/types'
{callExpression} = babel_types
{is_empty} = import '@fink/std-lib/iter.fnk'

{assign, lets, expr_block, undef} = import '../../js/types.fnk'
{transform_value} = import '../partial/init.fnk'
{wrap_with_comment_loc} = import '../comments/init.fnk'
{transform, collect_with_ctx} = import '../transform.fnk'



transform_pipe = fn node, ctx:
  {unique_ident} = ctx
  {exprs} = node

  [start_value, pipe_ctx] = match node.args:
    is_empty ?:
      [(undef _), ctx]
    else:
      [arg] = node.args
      transform arg, ctx

  result = unique_ident 'pipe_result'

  [pipe_calls, next_ctx] = pipe exprs:
    map expr, callee_ctx=pipe_ctx:
      [callee, next_ctx] = transform_value expr, callee_ctx

      js = wrap_with_comment_loc
        assign result, callExpression callee, [result]
        expr

      ([js, next_ctx], next_ctx)

    collect_with_ctx ctx

  js = expr_block
    wrap_with_comment_loc
      lets result, start_value
      start_value
    ...pipe_calls

  [js, next_ctx]