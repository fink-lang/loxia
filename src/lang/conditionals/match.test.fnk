{fink2js} = import '../../testing/generate.fnk'
{describe, it, expect, to_match_snapshot} = import '@fink/jest/test.fnk'


describe 'match', fn:
  it 'compiles', fn:
    expect
      fink2js '
        match shrub:
          # simple value
          123:
            # result
            spam

          # record matching
          {foo: 4, ni}:
            spam + ni

          # iterable matching
          [1, 2, ni]:
            ni

          # fallback if none of above match
          else:
            shrub
      '
      to_match_snapshot


  it 'compiles without else', fn:
    expect
      fink2js '
        match shrub:
          {foo: 4, ni}: spam + ni
          123: spam
      '
      to_match_snapshot


 it 'matches records', fn:
    expect
      fink2js "
        match shrub:
          # deep record comparison
          {foo: {}}: spam + ni
          {foo: []}: spam + ni
          {foo: 4, ni: {na, nu}}: spam + ni
          {xx: 1, foo: {bar: 'spam'}, shrub: {na: 'nu'}}: ni
          {ni: {len: 1}, na: {len: 1}}: na
          # calculated props
          {(a): {b, c}}: ni
          # spread
          {(foo): {}, ...{bar, spam}}: ni
      "
      to_match_snapshot


  it 'matches iterables', fn:
    expect
      fink2js '
        match shrub:
          # deep iterable comparison
          [1, 2, [ni]]: ni
          [1, 2, [], {}]: foo
          [1, [2, 3], 4]: bar
          [1, [2, [3, 4]], [5, 6], 7]: spam
      '
      to_match_snapshot

    expect
      # TODO: add support for asserting on rest spread
      fink2js '
        # spread and ignored elems
        match shrub:
          [1, ,2]: foo
          [1, [2, 3]]: foo
          [1, ..., 3]: foo
          [..., 2]: foo
      '
      to_match_snapshot


  it 'compiles nested match', fn:
    expect
      fink2js '
        test = fn:
          match foo:
            bar:
              match spam:
                shrub: ni
              bar
      '
      to_match_snapshot


  it 'compiles patial application', fn:
    expect
      fink2js '
        match {spam: 123}:
          # simple unary
          not ?: ni

          # simple binary
          ? > 123: ni

          # simple call
          ? (): ni
          ? _: ni

          # simple as call arg
          shrub ?: ni


          # nested unary
          {spam: not ?}: ni

          # nested binary
          {spam: ? > 123}: ni

          # nested call
          {spam: ? ()}: ni
          {spam: ? _}: ni

          # nested member call
          {spam: ?.is_foo ()}: ni
          {spam: ?.is_foo _}: ni

          # nested as call arg
          {spam: is_foo ?}: ni


          # nested unary
          [not ?]: ni

          # nested binary
          [? > 123]: ni

          # nested call
          [? ()]: ni
          [? _]: ni

          # nested member call
          [?.is_foo ()]: ni
          [?.is_foo _]: ni

          # nested as call arg
          [is_foo ?]: ni
      '
      to_match_snapshot


  it 'compiles do-expr', fn:
    expect
      fink2js '
        foo = match bar:
          spam:
            ni
            throw foobar
          else:
            shrub
            throw ni
      '
      to_match_snapshot

