babel_types = import '@babel/types'
{
  blockStatement, breakStatement, labeledStatement, objectPattern
  objectProperty, expressionStatement, ifStatement
  doExpression, stringLiteral, memberExpression, restElement
  identifier
} = babel_types
{length, is_empty} = import '@fink/std-lib/iter.fnk'

{and_, eq, not_nullish, typof, consts, unique_ident} = import '../../js/types.fnk'

{get_key} = import '../literals/record.fnk'
{is_partial} = import '../partial/init.fnk'
{wrap_with_comment_loc} = import '../comments/init.fnk'
{transform, map_with_ctx, collect_with_ctx} = import '../transform.fnk'



iter = fn value:
  symb = identifier 'Symbol'
  iterator = identifier 'iterator'
  symb_iter = memberExpression symb, iterator
  memberExpression value, symb_iter, true



is_iterable = fn value:
  and_
    not_nullish value
    eq
      typof iter value
      stringLiteral 'function'



comp = fn id, expected, ctx:
  value_ctx = {...ctx, partial_ident: id}
  # TODO: test how deep `?` goes
  [value, {partial_ident: _, ...next_ctx}] = transform expected, value_ctx

  cond = match expected:
    is_partial ?: value
    else: eq id, value

  [cond, next_ctx]



match_props = fn props, emit_result, ctx, cond:
  [{id, prop}, ...rest] = props

  value = match prop:
    ---istanbul ignore next---
    {type: 'spread'}:
      prop
    {left: {type: 'spread'}}:
      prop.left
    {right: false}:
      prop.left
    else:
      prop.right

  emit = match props:
    1 == length ?:
      emit_result
    else:
      fn ctx: match_props rest, emit_result, ctx, cond

  match_condition id, value, emit, ctx, cond



match_record = fn value, record, emit_result, ctx, cond:
  [id_props, props_ctx] = pipe record.exprs:
    map_with_ctx fn prop, id_ctx:
      [id, next_ctx] = unique_ident 'p', id_ctx
      [{id, prop}, next_ctx]
    collect_with_ctx ctx

  [props, result_ctx] = pipe id_props:
    map_with_ctx fn {id, prop}, prop_ctx:
      match prop:
        ---istanbul ignore next---
        {type: 'spread'}:
          [(restElement id), prop_ctx]
        {left: {type: 'spread'}}:
          [(restElement id), prop_ctx]
        else:
          [computed, key, next_ctx] = get_key prop, prop_ctx
          js = objectProperty key, id, computed
          [js, next_ctx]
    collect_with_ctx props_ctx

  decl = objectPattern props

  [result, end_ctx] = match id_props:
    is_empty ?:
      emit_result ctx
    else:
      [prop_match, next_ctx] = match_props
        id_props, emit_result, result_ctx, cond
      js = blockStatement list:
        consts decl, value
        prop_match
      [js, next_ctx]

  js = ifStatement
    not_nullish value
    result

  [js, end_ctx]



match_elems = fn elems, emit_result, ctx, cond:
  [{id, value}, ...rest] = elems

  emit = match elems:
    1 == length ?:
      emit_result
    else:
      fn ctx: match_elems rest, emit_result, ctx, cond

  match_condition id, value, emit, ctx, cond



get_array_decl = fn arr, id_elems, right, ctx:
  [...exprs] = pipe id_elems:
    map {id, value}:
      match value:
        # e.g. empty elems in [,, foo, bar]
        {type: 'empty'}:
          value

        # e.g. spread in [foo, ..., bar]
        {type: 'spread', right: false}:
          value

        # e.g. spread in [foo, ...bar, spam]
        {type: 'spread', right: {}}:
          ident = {type: 'ident', value: id.name}
          {...value, right: ident}

        else:
          {type: 'ident', value: id.name}

  left = {type: 'list', exprs, loc: arr.loc}

  [decl, next_ctx] = transform
    rec:
      type: 'assign'
      op: '='
      left
      right: {type: 'ident', value: right.name, loc: arr.loc}
      loc: arr.loc
    ctx

  js = consts decl.left, decl.right
  [js, next_ctx]



match_array = fn value, arr, emit_result, ctx, cond:
  [id_elems, elems_ctx] = pipe arr.exprs:
    map_with_ctx fn expr, expr_ctx:
      [id, next_ctx] = unique_ident 'a', expr_ctx
      [{id, value: expr}, next_ctx]
    collect_with_ctx ctx

  [array_decl, result_ctx] = get_array_decl arr, id_elems, value, elems_ctx

  [...filtered_id_elems] = pipe id_elems:
    filter {value}:
      match value:
        # empty elements need no matching
        {type: 'empty'}: false

        # empty spreads need no matching
        {type: 'spread', right: false}:  false

        {type: 'spread', right: {}}: true

        else: true

  [result, end_ctx] = match id_elems:
    is_empty ?:
      emit_result result_ctx
    else:
      [matched, next_ctx] = match_elems
        filtered_id_elems, emit_result, result_ctx, cond
      js = blockStatement [array_decl, matched]
      [js, next_ctx]

  js = ifStatement
    is_iterable value
    result
  [js, end_ctx]



match_simple = fn value, expr, emit_result, ctx:
  [cond, result_ctx] = comp value, expr, {...ctx, wrap: 'loc'}
  [result, next_ctx] = emit_result result_ctx
  js = ifStatement cond, result
  [js, next_ctx]



match_condition = fn value, expr, emit_result, ctx, cond:
  [js_expr, next_ctx] = match expr:
    {type: 'rec'}:
      match_record value, expr, emit_result, ctx, cond

    {type: 'list'}:
      match_array value, expr, emit_result, ctx, cond

    {type: 'spread'}:
      match_condition value, expr.right, emit_result, ctx, cond

    else:
      match_simple value, expr, emit_result, ctx

  js = wrap_with_comment_loc js_expr, cond
  [js, next_ctx]



split_condition = fn {left, right}:
  [left, right]



match_all = fn value, matches, emit, ctx:
  [exprs, end_ctx] = pipe matches:
    map_with_ctx fn expr, expr_ctx:
      match expr:
        {type: 'block', op: 'else'}:
          {op: _, ...block} = expr
          [js_expr, next_ctx] = emit block, expr_ctx
          js = wrap_with_comment_loc js_expr, block
          [js, next_ctx]
        else:
          [condition, result] = split_condition expr
          emit_result = fn ctx: emit result, ctx
          match_condition value, condition, emit_result, expr_ctx, condition
    collect_with_ctx ctx

  [exprs, end_ctx]



result_emitter = fn break_lbl: fn result, ctx:
  [result_js, next_ctx] = transform result, ctx
  js = blockStatement list:
    expressionStatement result_js
    breakStatement break_lbl

  [js, next_ctx]



transform_match = fn node, ctx:
  {exprs} = node
  [inputs] = node.args

  [value, match_ctx] = unique_ident 'value', ctx
  [break_lbl, inputs_ctx] = unique_ident 'match', match_ctx

  emit_result = result_emitter break_lbl

  [inputs_js, next_ctx] = transform inputs, inputs_ctx
  [match_exprs, end_ctx] = match_all value, exprs, emit_result, next_ctx

  js = doExpression
    blockStatement list:
      labeledStatement
        break_lbl
        blockStatement list:
          consts value, inputs_js
          ...match_exprs

  [js, end_ctx]
