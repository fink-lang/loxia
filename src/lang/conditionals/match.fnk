{
  arrayPattern, blockStatement, breakStatement, labeledStatement, objectPattern,
  objectProperty, isObjectExpression, isArrayExpression, expressionStatement,
  identifier
} = import '@babel/types'

{
  member, str, for_of, split_last, and, eq, not_nullish, typof,
  expr_block, consts, iff
} = import '../../js/types'

{get_key} = import '../literals/object'
{is_array} = import '../literals/array'


iter = fn value:
  member(value)(member(identifier('Symbol'))(identifier('iterator')), true)


is_iterable = fn value:
  and(
    not_nullish(value),
    eq(
      typof(iter(value)), str('function')
    )
  )


args_has_partial = fn {args}:
  pipe args:
    map arg: is_partial:: arg
    find partial: partial


is_partial = fn node:
  match node:
    {type: 'partial'}: true
    {left: {type: 'partial'}}: true
    {right: {type: 'partial'}}: true
    {type: 'call', callee: {type: 'partial'}}: true
    {type: 'call', callee: {left: {type: 'partial'}}}: true
    {type: 'call'}: args_has_partial:: node
    else: false


transform_with_partials = fn id, node, {partial_ident, transform, ...ctx}:
  partial = is_partial:: node

  value_ctx = {...ctx, partial_ident: id}

  # TODO: should get partial info from transforming node?
  [partial, transform(node, value_ctx)]


comp = fn id, expected, ctx:
  [partial, value] = transform_with_partials(id, expected, ctx)

  match true:
    partial: value
    else: eq(id, value)


split_simple_nested = fn id_props, use_val, ctx:
  pipe id_props:
    fold [id, item], [simple, nested]=[[], []]:
      elem = match true:
        use_val:
          match item.value:
            {type: 'block', exprs: {length: 1}}:
              {exprs: [expr]} = item.value
              expr
            else:
              item.value
        else: item

      match elem:
        {type: 'array'}:
          [simple, [...nested, {elem, id}]]
        {type: 'object'}:
          [simple, [...nested, {elem, id}]]
        else:
          [[...simple, {id, elem}], nested]


match_props = fn value, props, emit_result, ctx:
  [...id_props] = pipe props:
    map prop:
      id = ctx.unique_ident('p')
      [id, prop]

  decl = objectPattern(
    id_props.map(fn [id, prop]:
      [key, computed] = get_key(prop, ctx)
      objectProperty(key, id, computed)
    )
  )

  [simple, nested] = split_simple_nested(id_props, true, ctx)

  nested_block = match nested:
    {length: 0}:
      emit_result()
    else:
      blockStatement(
        nested.map(fn {elem, id}:
          match_condition(id, elem, emit_result, ctx)
        )
      )

  blockStatement([
    consts(decl, value),
    match simple:
      {length: 0}:
        nested_block
      else:
        iff(and(...simple.map(fn {elem, id}: comp(id, elem, ctx))))(
          nested_block
        )
  ])


match_obj = fn value, expr, emit_result, ctx:
  iff(not_nullish(value))(
    match_props(value, expr.exprs, emit_result, ctx)
  )


match_elems = fn value, elems, emit_result, ctx:
  [...id_elems] = pipe elems:
    map elem:
      id = ctx.unique_ident('a')
      [id, elem]

  decl = arrayPattern(id_elems.map(fn [id]: id))

  [simple, nested] = split_simple_nested(id_elems, false, ctx)

  blockStatement([
    consts(decl, value),

    iff(and(...simple.map(fn {elem, id}: comp(id, elem, ctx))))(
      match nested:
        {length: 0}:
          emit_result()
        else:
           blockStatement(
            nested.map(fn {elem, id}:
              match_condition(id, elem, emit_result, ctx)
            )
          )
    )
  ])


match_array = fn value, expr, emit_result, ctx:
  iff(is_iterable(value))(
    match_elems(value, expr.exprs, emit_result, ctx)
  )


match_simple = fn value, expr, emit_result, ctx:
  {
    ...iff(comp(value, expr, ctx))(
      emit_result()
    ),
    loc: expr.loc
  }


match_condition = fn value, expr, emit_result, ctx:
  match expr:
    {type: 'object'}:
      match_obj(value, expr, emit_result, ctx)

    {type: 'array'}:
      match_array(value, expr, emit_result, ctx)

    else:
      match_simple(value, expr, emit_result, ctx)


split_condition = fn {left, right}:
  [left, right]


match_all = fn value, matches, emit_result, ctx:
  pipe matches:
    map expr:
      [condition, result] = split_condition(expr)

      match condition:
        {value: 'else'}:
          emit_result(result)
        else:
          match_condition(value, condition, fn: emit_result(result), ctx)


transform_match = fn node, ctx:
  {exprs} = node
  [inputs] = node.args

  value = ctx.unique_ident('value')
  break_lbl = ctx.unique_ident('match')

  emit_result = fn result:
    blockStatement([
      expressionStatement(ctx.transform(result)),
      breakStatement(break_lbl)
    ])

  expr_block(
    labeledStatement(break_lbl,
      blockStatement([
        consts(value, ctx.transform(inputs)),
        ...match_all(value, exprs, emit_result, ctx)
      ])
    )
  )

