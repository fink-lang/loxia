{is_fn} = import '@fink/std-lib/fn.fnk'

{add_builtin, add_default_builtin} = import './builtins.fnk'


any = false


add = fn type, op, transformer: fn {transformers, ...ctx}:
  next_transformers = dict:
    ...transformers
    (type or op): transformer

  {transformers: next_transformers, ...ctx}



add_with_builtin = fn type, op, impl_uri, transformer: fn ctx:
  built_in_name = '_${op}_'
  pipe ctx:
    add_builtin built_in_name, ?
    add_default_builtin built_in_name, impl_uri, ?
    add type, op, transformer



get_transformer = fn {op, type}, {transformers}:
  match transformers:
    {(op): is_fn ?}:
      {(op): transform} = transformers
      transform

    {(type): is_fn ?}:
      {(type): transform} = transformers
      transform

    else:
      false
