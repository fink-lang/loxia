babel_traverse = import '@babel/traverse'
{default: traverse} = babel_traverse

{transformFromAstSync} = import '@babel/core'
{try_catch} = import '@fink/js-interop/errors.js'
{set_props} = import '@fink/js-interop/reflect.js'
{undefined} = import '@fink/js-interop/nullish.js'

{transform} = import './ir/transform.fnk'
{init_ctx} = import './ir/init.fnk'
{optimize} = import './optimize/init.fnk'
{transform_ir} = import './js/init.fnk'
{module_transforms} = import './js/module/init.fnk'
{transform_async} = import './js/async/init.fnk'


# try_catch_ = fn f: [false, f _]


transform_file = fn fink_ast, code, filename, {optimize: optim, ...options}:
  opts = {...options, optimize: {refs: true, tails: true, unused: true, ...optim}}

  ctx = init_ctx code, filename, opts

  [error, [js_ast]=[]] = try_catch fn:
    [lir] = transform fink_ast, 'mod', ctx
    [olir] = optimize lir, opts
    {js} = transform_ir olir, opts
    [js]

  extras = match options:
    {module_type: 'cjs'}: module_transforms
    else: {}

  match error:
    false:
      traverse js_ast, rec:
        ReturnStatement: fn path:
          match path:
            {key: ? > 0}:
              set_props path.node, rec:
                leadingComments: [{type: 'CommentBlock', value: ' istanbul ignore next '}]
              undefined

        AwaitExpression: {enter: transform_async}
        ...extras

      {...js_ast, errors: []}
    else:
      {errors: [error]}



babel_generate = fn ast, filename, source, options:
  {babel={babelrc: false, configFile: false}, source_maps} = options

  babel_opts = rec:
    filename
    sourceFileName: filename
    sourceMaps: source_maps
    ...babel

  [error, result] = try_catch fn:
    {code, map: source_map} = transformFromAstSync ast, source, babel_opts
    {code, source_map, errors: []}

  match error:
    false:
      result
    else:
      {code: '', source_map: '', errors: [error]}



generate = fn ast, filename, source, options={}:
  js_ast = transform_file ast, source, filename, options

  match js_ast:
    {errors: [?]}:
      {code: '', source_map: '', errors: js_ast.errors}
    else:
      babel_generate js_ast, filename, source, options

