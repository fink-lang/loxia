{rx , matches, replace} = import '@fink/std-lib/regex.fnk'



acceptable_idents = rx'
  ^(?!(?:
    do|if|in|for|let|new|try|var|case|enum|void|with|break|catch|class|
    const|super|while|yield|delete|export|import|public|return|static|switch|
    typeof|default|extends|finally|package|private|continue|function|
    arguments|interface|protected|implements|instanceof|null|undefined
  )$)
  [_$\p{L}][_$\p{L}\p{N}]*$'


# valid hyphen replacements for js idents [ˉ, ـ, ᅳ, ￚ, ᐨ, ᜭ]
hyphen_replacement = 'ᜭ'


escape_hyphen = fn name:
  replace name, rx'-', hyphen_replacement


is_js_ident = fn ident:
  matches ident, acceptable_idents


escape_ident = fn ident:
  name = escape_hyphen ident

  match name:
    matches ?, acceptable_idents:
      name
    else:
      'ˆ${name}'



replace_refs = fn [id=false, ...ids], renames, out=[]:
  match id:
    false:
      out
    else:
      {(id): new_id=escape_ident id} = renames
      replace_refs ids, renames, [...out, new_id]



rename = fn [id=false, ...ids], renames, out=[]:
  match id:
    false:
      [out, renames]
    else:
      new_id = escape_ident id
      nrs = {...renames, (id): new_id}
      rename ids, nrs, [...out, new_id]



safe_names = fn [expr=false, ...exprs], renames={}, out=[]:
  match expr:
    false: [out, renames]

    [? in ['int', 'float', 'str']]:
      [op, args, res, ...rest] = expr
      [nres, next_ren] = rename res, renames
      safe_names exprs, next_ren, [...out, [op, args, nres, ...rest]]


    [? in ['fn', 'fnr', 'cn']]:
      [op, [args, body], res, ...rest] = expr
      [nargs, bod_ren] = rename args, renames
      [nbody, res_ren] = safe_names body, bod_ren
      [nres, next_ren] = rename res, res_ren
      safe_names exprs, next_ren, [...out, [op, [nargs, nbody], nres, ...rest]]

    else:
      [op, args, res, ...rest] = expr
      nargs = replace_refs args, renames
      [nres, next_ren] = rename res, renames
      safe_names exprs, next_ren, [...out, [op, nargs, nres, ...rest]]



replace_idents = fn exprs:
  [out] = safe_names exprs
  out