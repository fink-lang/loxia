types = import '@babel/types'
{
  file, program, exportNamedDeclaration, identifier
  assignmentExpression, expressionStatement
  memberExpression, callExpression, objectProperty, objectPattern
  variableDeclaration, variableDeclarator
  isImportDefaultSpecifier, isImportSpecifier, interpreterDirective
} = types

{map, fold} = import '@fink/std-lib/iter.fnk'
{slice} = import '@fink/std-lib/str.fnk'


{add, get_js, set_js2, get_js_literal, with_loc} = import '../context.fnk'
{transform_exprs} = import '../transform.fnk'
{escape_ident} = import '../identifier/init.fnk'
{get_runtime_imports} = import './import.fnk'


[no_result] = []


transform_specifiers = fn {specifiers, source}:
  init = callExpression
    identifier 'require'
    [source]

  [first] = specifiers

  match first:
    isImportDefaultSpecifier ?:
      [{local}] = specifiers
      variableDeclaration 'const', [variableDeclarator local, init]

    isImportSpecifier ?:
      props = pipe specifiers:
        map fn node:
          objectProperty
            node.imported
            node.local
            false
            node.imported.name == node.local.name
        [...?]

      variableDeclaration 'const', list:
        variableDeclarator
          objectPattern [...props]
          init

    else:
      expressionStatement init



module_transforms = rec:
  ImportDeclaration: fn path:
    path.replaceWith transform_specifiers path.node
    no_result

  ExportNamedDeclaration: fn path:
    {declaration: {declarations}} = path.node
    exports = identifier 'exports'

    pipe declarations:
      fold fn {id, init}:
        path.insertAfter
          expressionStatement
            assignmentExpression
              '='
              memberExpression exports, id
              init

    path.remove _
    no_result



transform_mod_exports = fn expr, ctx:
  [{args: [exports_id]}] = expr

  decls = pipe get_js exports_id, ctx:
    ?.properties
    map fn {key, value}:
      name = match key:
        {type: 'StringLiteral'}: key.value
        else: key.name

      ident = with_loc key, identifier escape_ident name
      with_loc key, variableDeclarator ident, value
    [...?]

  js = exportNamedDeclaration variableDeclaration 'const', decls

  set_js2 expr, js, ctx




transfrom_module = fn exprs, ctx:
  [body, next_ctx] = transform_exprs exprs, ctx
  [..., last] = exprs
  [{args: [ , drctvs_id]}] = last

  drctvs = get_js_literal drctvs_id, next_ctx

  directives = match drctvs:
    {elements: [{type: 'TemplateLiteral'}]}:
      [{quasis: [{value: {raw: value}}]}] = drctvs.elements
      [interpreterDirective slice value, 2]
    else:
      []

  runtime_imports = get_runtime_imports next_ctx

  js = file
    program
      [...runtime_imports, ...body]
      []
      'module'
      ...directives

  {...next_ctx, js}



add_module = fn ctx:
  pipe ctx:
    add 'mod', transform_mod_exports


