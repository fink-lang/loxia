{init_ctx} = import '../ir/context.fnk'
{optimize_unused} = import './unused.fnk'
{optimize_refs} = import './refs.fnk'
{optimize_tails} = import './tail-calls.fnk'
{optimize_names} = import './short-ids.fnk'



maybe_opt_refs = fn ir, ctx:
  match ctx:
    {optimize.refs: true}:
      optimize_refs ir, ctx
    else:
      [ir, ctx]



maybe_opt_tails = fn ir, ctx:
  match ctx:
    {optimize.tails: true}:
      optimize_tails ir, ctx
    else:
      [ir, ctx]



maybe_opt_unused = fn ir, ctx:
  match ctx:
    {optimize.unused: true}:
      pipe ctx:
        init_ctx ir, ?
        optimize_unused ir, ?
    else:
      [ir, ctx]



maybe_opt_names = fn ir, ctx:
  match ctx:
    {optimize.names: true}:
      optimize_names ir, ctx
    else:
      [ir, ctx]



optimize = fn exprs, options:
  ctx = init_ctx exprs, options
  pipe [exprs, ctx]:
    maybe_opt_refs ...?
    maybe_opt_tails ...?
    maybe_opt_unused ...?
    maybe_opt_names ...?


