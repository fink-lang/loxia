{reverse} = import '@fink/std-lib/iter.fnk'
{transform} = import '../transform.fnk'
{unique_or_id, ir_fn} = import '../context.fnk'

{str} = import './string.fnk'



rec_m = fn rec1_id, rec2_id, name_or_id, {loc}, ctx:
  ir_fn 'rec_m', [rec1_id, rec2_id], name_or_id, {loc}, ctx


rec_s = fn rec_id, key_id, val_id, name_or_id, {loc}, ctx:
  ir_fn 'rec_s', [rec_id, key_id, val_id], name_or_id, {loc}, ctx


rec_e = fn name_or_id, {loc}, ctx:
  ir_fn 'rec', [], name_or_id, {loc}, ctx



members_as_rec = fn expr, val:
  match expr:
    {type: 'member'}:
      kv = {type: 'rec:kv', left: expr.right, right: val}
      nval = {type: 'rec', exprs: [kv], loc: expr.loc}
      members_as_rec expr.left, nval
    else:
      kv = {type: 'rec:kv', left: expr, right: val}
      {type: 'rec', exprs: [kv], loc: expr.loc}



transform_key = fn expr, ctx:
  match expr:
    {type: 'ident'}:
      str expr.value, 'key', expr, ctx
    else:
      transform expr, 'key', ctx



transform_val = fn expr, key_id, ctx:
  match expr:
    {left: {type: 'ident'}, right: false}:
      transform expr.left, 'val', ctx

    {right: false}:
       [[], key_id, ctx]

    else:
      transform expr.right, 'val', ctx



transform_kv = fn expr, out_id, ctx:
  [rec_id, key_ctx] = unique_or_id 'rec', expr, ctx
  match expr:
    {type: 'spread'}:
      [val, val_id, rec_ctx] = transform expr.right, 'sprd', key_ctx
      [rec_exprs, , next_ctx] = rec_m rec_id, val_id, out_id, expr, rec_ctx
      [[...val, ...rec_exprs], rec_id , next_ctx]

    else:
      [key, key_id, val_ctx] = transform_key expr.left, key_ctx
      [val, val_id, rec_ctx] = transform_val expr, key_id, val_ctx
      [rec_exprs, , next_ctx] = rec_s rec_id, key_id, val_id, out_id, expr, rec_ctx
      [[...key, ...val, ...rec_exprs], rec_id, next_ctx]



transform_entries = fn [expr=false, ...rest], ctx, result_id, out=[]:
  match expr:
    false:
      [out, result_id, ctx]

    {left.type: 'member'}:
      {exprs} = members_as_rec expr.left, expr.right
      transform_entries [...exprs, ...rest], ctx, result_id, out

    else:
      [kv, rec_id, next_ctx] = transform_kv expr, result_id, ctx
      transform_entries rest, next_ctx, rec_id, [...kv, ...out]



transform_record = fn node, result, ctx:
  exprs = reverse node.exprs
  [out, rec_id, rec_ctx] = transform_entries exprs, ctx, result
  [rec_exprs, , next_ctx] = rec_e rec_id, node, rec_ctx
  [[...rec_exprs, ...out], next_ctx]

